buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

repositories {
    maven { url = 'https://maven.minecraftforge.net' }
    jcenter()
    mavenCentral()
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'maven-publish'

ext.modversion              = "5.1.0"
ext.mcversion               = "1.18.1"
ext.forgeversion_dependency = "39"
ext.forgeversion            = "39.0.0"

version = mcversion + "-" + modversion
group = "zsawyer.mods.mumblelink.MumbleLink"
archivesBaseName = "mumblelink"
ext.vendorName = "zsawyer." + archivesBaseName

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: mcversion

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                archivesBaseName {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                archivesBaseName {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                archivesBaseName {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
dependencies {
    minecraft 'net.minecraftforge:forge:' + mcversion + '-' + forgeversion
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : project.name,
            "Specification-Vendor"    : "${vendorName}",
            "Specification-Version"   : "${version}",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${version}",
            "Implementation-Vendor"   : "${vendorName}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    filesMatching(['**/mods.toml', '**/README.txt']) {
        expand 'version': version,
                'modversion': modversion,
                'mcversion': mcversion,
                'forgeversion': forgeversion,
                'forgeversion_dependency': forgeversion_dependency,
                'date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
